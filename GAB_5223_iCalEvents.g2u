Program.Sub.ScreenSU.Start
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
Program.Sub.Preflight.End

Program.Sub.Main.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare

	V.Local.iCt.Declare(Long)
	V.Local.iTime.Declare(Long)
	V.Local.sTimeZone.Declare(String)
	V.Local.sFileName.Declare(String)
	V.Local.sTitle.Declare(String)
	V.Local.sDesc.Declare(String)
	V.Local.sYear.Declare(String)
	V.Local.sMonth.Declare(String)
	V.Local.sDay.Declare(String)
	V.Local.sSTm.Declare(String)
	V.Local.sETm.Declare(String)
	V.Local.sHour.Declare(String)
	V.Local.sMin.Declare(String)
	V.Local.sMeri.Declare(String)
	V.Local.sStart.Declare(String)
	V.Local.sEnd.Declare(String)
	V.Local.bAllDay.Declare(Boolean)
	V.Local.bSingleDay.Declare(Boolean)
	V.Local.sAttendees.Declare(String)
	V.Local.sAttendeesMail.Declare(String)
	V.Local.sAttendeesNames.Declare(String)
	V.Local.sRecipients.Declare(String)
	V.Local.iUserID.Declare(Long)
	V.Local.sAttach.Declare(String)
	V.Local.sSender.Declare(String)
	V.Local.sMsg.Declare(String)
	V.Local.sSenderEmail.Declare(String)
	V.Local.bDaylightSavings.Declare(Boolean)
	
	'hook 42962 = 51080
	F.Intrinsic.Control.If(V.Caller.Hook,=,51080)
		'Set the Title/Summary
		V.Local.sTitle.Set(Variable.Passed.frmEvent-txtSubject)
		'Set the File Name- remove the characters that files names dont allow.
		F.Intrinsic.File.MakeFilenameFriendly(v.Local.sTitle,v.Local.sTitle)
		F.Intrinsic.String.Build("{0}\{1}.ics",V.System.Temp,V.Local.sTitle,V.Local.sFileName)
		'Set Description
		V.Local.sDesc.Set(Variable.Passed.frmEvent-tbDescription)
		
		'Set the Start Date
		V.Local.sStart.Set(Variable.Passed.frmEvent-edtStartDate)
		F.Intrinsic.String.Left(V.Local.sStart,4,V.Local.sYear)
		F.Intrinsic.String.Mid(V.Local.sStart,5,2,V.Local.sMonth)
		F.Intrinsic.String.Right(V.Local.sStart,2,V.Local.sDay)
		V.Local.bAllDay.Set(Variable.Passed.frmEvent-chkAllDay-v)
		F.Intrinsic.Control.If(V.Local.bAllDay,=,True)
			V.Local.sSTm.Set("00:00:00")
		F.Intrinsic.Control.Else
			V.Local.sSTm.Set(Variable.Passed.frmEvent-edtStartTime)
		F.Intrinsic.Control.EndIf
		F.Intrinsic.Date.DateSerial(V.Local.sYear,V.Local.sMonth,V.Local.sDay,V.Local.sStart)
		F.Intrinsic.String.Build("{0} {1}",V.Local.sStart.DateComp,V.Local.sSTm,V.Local.sStart)
	
		'Set the End-Date
		V.Local.bSingleDay.Set(Variable.Passed.frmEvent-chkAllDay-v)
		F.Intrinsic.Control.If(V.Local.bSingleDay,=,False)
			V.Local.sEnd.Set(Variable.Passed.frmEvent-edtEndDate)
			F.Intrinsic.String.Left(V.Local.sEnd,4,V.Local.sYear)
			F.Intrinsic.String.Mid(V.Local.sEnd,5,2,V.Local.sMonth)
			F.Intrinsic.String.Right(V.Local.sEnd,2,V.Local.sDay)
		F.Intrinsic.Control.EndIf
		F.Intrinsic.Control.If(V.Local.bAllDay,=,True)
			V.Local.sETm.Set("23:59:00")
		F.Intrinsic.Control.Else
			V.Local.sETm.Set(Variable.Passed.frmEvent-edtEndTime)
		F.Intrinsic.Control.EndIf
		F.Intrinsic.Date.DateSerial(V.Local.sYear,V.Local.sMonth,V.Local.sDay,V.Local.sEnd)
		F.Intrinsic.String.Build("{0} {1}",V.Local.sEnd.DateComp,V.Local.sETm,V.Local.sEnd)
		
		'Set Attendees list
		V.Local.sAttendeesNames.Set(Variable.Passed.frmEvent-lstbxRecipients)
		F.Intrinsic.String.Split(V.Local.sAttendeesNames,"|~|",V.Local.sAttendeesNames)
		F.Intrinsic.Control.For(V.Local.iCt,0,V.Local.sAttendeesNames.UBound,1)
			F.Global.Security.GetUserEmail(V.Local.sAttendeesNames(v.Local.iCt),V.Local.sAttendeesMail)
			F.Intrinsic.Control.If(V.Local.sAttendeesMail,<>,"")
				F.Intrinsic.Control.If(V.Local.sAttendees,=,"")
					V.Local.sAttendees.Set(V.Local.sAttendeesMail)
					F.Intrinsic.String.Build("{0}*!*{1}",V.Local.sAttendeesNames(v.Local.iCt),V.Local.sAttendeesMail,V.Local.sRecipients)
				F.Intrinsic.Control.Else
					F.Intrinsic.String.Build("{0}*!*{1}",V.Local.sAttendees,V.Local.sAttendeesMail,V.Local.sAttendees)
					F.Intrinsic.String.Build("{0}@!@{1}*!*{2}",V.Local.sRecipients,V.Local.sAttendeesNames(v.Local.iCt),V.Local.sAttendeesMail,V.Local.sRecipients)
				F.Intrinsic.Control.EndIf
			F.Intrinsic.Control.EndIf
		F.Intrinsic.Control.Next(V.Local.iCt)
		
		'Set TimeZone
		F.Intrinsic.Date.IsDST(V.Local.bDaylightSavings)
		F.Intrinsic.Date.UTCoffset(V.Local.bDaylightSavings,V.Local.iTime)
		F.Intrinsic.Control.If(V.Local.bDaylightSavings)
			F.Intrinsic.Control.SelectCase(V.Local.iTime)
				F.Intrinsic.Control.Case(-4)
					V.Local.sTimeZone.Set("EASTERN")
				F.Intrinsic.Control.Case(-5)
					V.Local.sTimeZone.Set("CENTRAL")
				F.Intrinsic.Control.Case(-6)
					V.Local.sTimeZone.Set("MOUNTAIN")
				F.Intrinsic.Control.Case(-7)
					V.Local.sTimeZone.Set("PACIFIC")
				F.Intrinsic.Control.CaseElse
					F.Intrinsic.Control.ExitSub
			F.Intrinsic.Control.EndSelect
		F.Intrinsic.Control.Else
			F.Intrinsic.Control.SelectCase(V.Local.iTime)
				F.Intrinsic.Control.Case(-5)
					V.Local.sTimeZone.Set("EASTERN")
				F.Intrinsic.Control.Case(-6)
					V.Local.sTimeZone.Set("CENTRAL")
				F.Intrinsic.Control.Case(-7)
					V.Local.sTimeZone.Set("MOUNTAIN")
				F.Intrinsic.Control.Case(-8)
					V.Local.sTimeZone.Set("PACIFIC")
				F.Intrinsic.Control.CaseElse
					F.Intrinsic.Control.ExitSub
			F.Intrinsic.Control.EndSelect
		F.Intrinsic.Control.EndIf
		
		'Create iCal
		F.Automation.Groupware.CreateiCalEvent(V.Local.sFileName,V.Local.sTitle,V.Local.sDesc,V.Local.sStart,V.Local.sEnd,V.Local.sTimeZone,V.Local.sAttendees)
		
		'Format and Send Email
		F.Global.Security.GetUserEmail(V.Caller.User,V.Caller.CompanyCode,V.Local.sSenderEmail)
		F.Global.Security.GetUserID(V.Caller.User,V.Caller.CompanyCode,V.Local.iUserID)
		F.Intrinsic.String.Build("{0}*!*{1}",V.Local.sSenderEmail,V.Caller.User,V.Local.sSender)
		F.Intrinsic.String.Build("{0}.ics*!*{1}",V.Local.sTitle,V.System.Temp,V.Local.sAttach)
		
		F.Intrinsic.Control.If(V.Local.sSenderEmail,=,"")
			F.Intrinsic.String.Build("Event ""{0}"" can not be Sent{1}Senders Email ID could not be found",V.Local.sTitle,V.Ambient.NewLine,V.Local.sMsg)
			F.Intrinsic.UI.Msgbox(V.Local.sMsg,"CRM Events")
		F.Intrinsic.Control.Else
			F.Global.Messaging.QueueMessage(V.Caller.CompanyCode,V.Local.iUserID,V.Caller.Caller,V.Local.sTitle,V.Local.sSender,V.Local.sRecipients,V.Local.sDesc,-1,V.Local.sSenderEmail,False,"","","","","","","",V.Local.sAttach,True)
		F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.EndIf

	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.End 
	F.Intrinsic.Control.EndTry
Program.Sub.Main.End